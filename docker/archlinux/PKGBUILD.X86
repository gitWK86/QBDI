# Maintainer: Charles Hubain <chubain@quarkslab.com>

pkgname=QBDI-X86
pkgver=0.7.2_devel
pkgrel=1
epoch=
pkgdesc="QuarkslaB Dynamic binary Instrumentation for linux-X86"
arch=('x86_64' 'i686')
url="https://qbdi.quarkslab.com/"
license=('apache2')
depends=('lib32-zlib')
options=(staticlibs)
makedepends=('cmake' 'make' 'gcc' 'python')

prepare() {
    mkdir "$pkgname-$pkgver"
}

build() {
    cd "$pkgname-$pkgver"
    cmake -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_CROSSCOMPILING=FALSE \
          -DQBDI_PLATFORM=linux \
          -DQBDI_ARCH=X86 \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DQBDI_TOOLS_PYQBDI=OFF \
          ../../../
    [[ -d "../../../deps/llvm/linux-X86" ]] || make llvm
    [[ -d "../../../deps/gtest/linux-X86" ]] || make gtest
    cmake -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_CROSSCOMPILING=FALSE \
          -DQBDI_PLATFORM=linux \
          -DQBDI_ARCH=X86 \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DQBDI_TOOLS_PYQBDI=OFF \
          ../../../
    make -j4
}

check() {
    cd "$pkgname-$pkgver"
    ./test/QBDITest
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}
